plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id "org.jooq.jooq-codegen-gradle" version "3.20.10"
}

group = 'com.gistacsl'
version = '0.0.1-SNAPSHOT'
description = 'mosaic'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    jooq_generated_location = 'build/generated-sources/jooq'
    jooq_package = 'com.gistacsl.mosaic.jooq'
}

sourceSets {
    main {
        java {
            srcDir jooq_generated_location
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation('org.springframework.boot:spring-boot-starter-webflux') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'

    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.jooq:jooq:3.20.10'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    jooqCodegen 'org.jooq:jooq-meta-extensions:3.20.10'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'

    // Source: https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-api
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.79'

    testImplementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.79'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'io.projectreactor:reactor-test'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('deleteGeneratedFolder', Delete) {
    delete jooq_generated_location
}

jooqCodegen {
    dependsOn deleteGeneratedFolder
}

compileJava {
    dependsOn jooqCodegen
}

jooq {
    configuration {
        generator {
            name = "org.jooq.codegen.JavaGenerator"
            database {
                name = "org.jooq.meta.extensions.ddl.DDLDatabase"
                properties {

                    // Specify the location of Flyway migration scripts
                    // Use V*.sql pattern to include only versioned migrations (excludes local seed data)
                    property {
                        key = "scripts"
                        value = "src/main/resources/db/migration/V*.sql"
                    }

                    // Sort migration files to ensure they are applied in correct order
                    property {
                        key = "sort"
                        value = "flyway"
                    }

                    // The default name case for unquoted objects:
                    //
                    // - as_is: unquoted object names are kept unquoted
                    // - upper: unquoted object names are turned into upper case (most databases)
                    // - lower: unquoted object names are turned into lower case (e.g. PostgreSQL)
                    property {
                        key = "defaultNameCase"
                        value = "lower"
                    }
                }
            }
            target {
                packageName = jooq_package
                directory = jooq_generated_location
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
